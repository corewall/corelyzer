apply plugin: 'java'

sourceCompatibility = 1.11
targetCompatibility = 1.11

jar.doFirst {
	manifest {
		attributes("Implementation-Title": "Corelyzer Application",
        	"Implementation-Version": version)
    }
} 
repositories {
	mavenCentral()
	maven {url 'https://maven.java.net/service/local/staging/deploy/maven2/'}
	maven {url 'https://repo.opennms.org/maven2/'}
	flatDir name: 'local', dirs: file('deps')
}

configurations {
	all*.exclude group:'asm'
	all*.exclude group:'ant'
	all*.exclude group: 'xml-apis'
}

dependencies {
	compile project(':scenegraph')
	compile 'javax.help:javahelp:2.0.02'
	compile 'rome:rome:1.0'
	compile 'xerces:xercesImpl:2.9.1' // use standard XML APIs?
	compile 'com.intellij:javac2:7.0.3' // switch to MigLayout?
	compile 'jexcelapi:jxl:2.6' // switch to POI?
	compile 'net.java.dev.swing-layout:swing-layout:1.0.2' // switch to MigLayout?
	compile 'com.miglayout:miglayout:3.7.4' // brg
	compile 'net.java.dev.glazedlists:glazedlists_java15:1.8.0' // brg - required by iCores
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'commons-codec:commons-codec:1.2' // remove dependency
	compile 'install4j:i4jruntime:3.2.4' // do we need this?
	compile ':jdc:1.0.0' // switch to standard directory chooser?
	compile ':jai_core:1.1.3'
	compile ':jai_codec:1.1.3'
	compile ':jai_imageio:1.1'
	compile ':clibwrapper_jiio:1.1'
	compile 'ch.qos.logback:logback-core:1.0.6'
	compile 'ch.qos.logback:logback-classic:1.0.6'

	// brg 3/31/2022
	// There is a newer release of JOGL 2.4 that might be worth trying with regard to Windows
	// z-order bugs (popup, dialogs drawing behind canvas) in addition to other bugfixes:
	// https://jogamp.org/deployment/v2.4.0-rc-20210111/
	// Unclear if/when they'll ever do an official release of 2.4, so will need to use
	// approach described below (download and place in app/deps) until they do.


	// brg 6/24/2020
	// JOGL version 2.4 release candidates are required to resolve launch-time crash in macOS/OSX:
	// "Terminating app due to uncaught exception 'NSInternalInconsistencyException', reason:
	// 'NSWindow drag regions should only be invalidated on the Main Thread!'"
	// Currently using JARs downloaded from v2.4.0-rc-20200307/ dir here, and placed in app/deps/:
	// https://jogamp.org/deployment/archive/rc/
	compile group: 'org.jogamp.gluegen', name: 'gluegen-rt'
	compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-natives-windows-amd64'
	compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-natives-macosx-universal'
	compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-natives-linux-amd64'
	compile group: 'org.jogamp.jogl', name: 'jogl-all'
	compile group: 'org.jogamp.jogl', name: 'jogl-all-natives-windows-amd64'
	compile group: 'org.jogamp.jogl', name: 'jogl-all-natives-macosx-universal'
	compile group: 'org.jogamp.jogl', name: 'jogl-all-natives-linux-amd64'

	compile group: 'com.apple', name: 'dnssd', version: '108.6'
	compile('net.java.dev.stax-utils:stax-utils:20070216') {
		exclude group:'com.bea.xml'
	}
	compile 'org.json:json:20140107'
	compile('com.brsanthu:google-analytics-java:1.1.2')
	compile 'com.opencsv:opencsv:5.0'
}

clean.doLast {
	ant.delete(dir: file('dist'), failonerror: false)
	ant.delete(dir: file('bin'), failonerror: false)
}

build.doLast {
	copy {
		into file('dist')
		from configurations.default
		from configurations.default.allArtifacts.getFiles()
	}

	// To build and debug, VSCode seems to depend on contents of build/classes/java
	// being copied into app/bin dir, but doesn't always do so on its own, unclear why.
	// Copy manually here.
	mkdir('bin')
	copy {
		into file('bin')
		from 'build/classes/java'
	}
}
